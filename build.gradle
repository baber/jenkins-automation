apply plugin: 'groovy'

ext {
    jobDslVersion = '1.65'
    jenkinsVersion = '2.19'
}

repositories {
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
    jcenter()
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    compile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

dependencies {

    compile 'org.codehaus.groovy:groovy-all:2.4.10'

    compile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'

    compile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'

    // Jenkins test harness dependencies
    compile group: 'org.jenkins-ci.main', name: 'jenkins-test-harness', version: '2.29'
    compile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    compile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    compile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    compile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    compile 'org.jenkins-ci.plugins:structs:1.6@jar'

    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.31.4'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'
    testPlugins 'org.jenkins-ci.plugins:s3:0.10.12'
    testPlugins 'org.jenkins-ci.main:maven-plugin:3.0'
    testPlugins 'org.jenkins-ci.plugins:aws-java-sdk:1.11.119'
    testPlugins 'org.jenkins-ci.plugins:copyartifact:1.38.1'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.16'

}

task resolveTestPlugins(type: Copy) {


    from configurations.testPlugins {
        include '*.hpi'
        include '*.jpi'
    }

    from('./src/test/resources/plugins') {
        include '*.hpi'
    }

    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    doLast {
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    useJUnit {
        includeCategories 'categories.Validation'
    }

    dependsOn tasks.resolveTestPlugins
    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}


task loadSeed(type: Test) {

    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = true

    useJUnit {
        includeCategories 'categories.Deploy'
    }

    dependsOn tasks.resolveTestPlugins

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath

}